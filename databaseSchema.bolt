/**
 * This Bolt file gets compiled into the database.rules.json file for the Firebase Realtime
 * Database. There are libraries that claim to be able to generate typescript definitions that
 * correspond to this schema, but neither of the two that I tried were good enough, so the
 * TypeScript definitions are maintained by hand in functions/apiContract/database/DataModel.ts.
 */

path / {
  write() { false }
}

path /publicGameState/{gameId} is PublicGameState {
  read() { true }
}

path /publicGameConfig/{gameId} is PublicGameConfig {
  read() { true }
}

path /publicGameStateJson/{gameId} is String {
  read() { true }
}

// Game events are (for now) private to the server
path /gameEventsJson/{gameId} is GameEvents {
  read() { false }
}

// Player identities are private to the server
path /playerIdentities/{gameId} is Map<Position, String> {
  read() { false }
}

// Player-private game state can be read only by that player.
// Since a player knows only their own ID, this is "security by obscurity".
path /playerPrivateGameState/{gameId}/{playerId} is PlayerPrivateGameState {
  read() { true }
}

type PublicGameState {
  score: Map<Partnership, Number>,
  currentDealer: Position,
  bids: Map<Position, Bid>,
  trump: Suit | Null,
  currentTrickLead: Position,
  currentTrick: Map<Position, Card | Null>,
  wonTricksThisRound: Map<Partnership, Number>,
}

type PublicGameConfig {
  gameExists: Boolean,
  playerFriendlyNames: Map<Position, String>,
}

type GameEvents extends String[] {}

type PlayerPrivateGameState {
  hand: Card[],
}

type Suit extends String {
  validate() {
    this === 'C' ||
    this === 'H' ||
    this === 'S' ||
    this === 'D'
  }
}

type Rank extends String {
  validate() {
    this === '9' ||
    this === '10' ||
    this === 'J' ||
    this === 'Q' ||
    this === 'K' ||
    this === 'A'
  }
}

type Card {
  rank: Rank,
  suit: Suit
}

type Position extends String {
  validate() {
    this === 'north' ||
    this === 'south' ||
    this === 'east' ||
    this === 'west'
  }
}

type Partnership extends String {
  validate() { this === 'northsouth' || this === 'eastwest' }
}

type Bid extends String | Number | Null {
  validate() {
    this === 1 ||
    this === 2 ||
    this === 3 ||
    this === 4 ||
    this === 5 ||
    this === 6 ||
    this === 12 ||
    this === 24 ||
    this === 48 ||
    this === 96 ||
    this === 192 ||
    this === null ||
    this === 'pass'
  }
}
